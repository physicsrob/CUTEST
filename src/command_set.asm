;
;
;
;
;      "SET" COMMAND
;
;   THIS ROUTINE GETS THE ASSOCIATED PARAMETER AND
;   DISPATCHES TO THE PROPER ROUTINE FOR SETTING
;   MEMORY VALUES.
;
CSET:	CALL	SBLK	;SCAN TO SECONDARY COMMAND
	JZ	ERR1	;MUST HAVE AT LEAST SOMETHING!!
	PUSH	D	;SAVE SCAN ADDRESS
	CALL	SCONV	;CONVERT FOLLOWING VALUE
	XTHL		;HL=SAVED SCAN ADDR AND STACK=VALUE
	LXI	D,SETAB	;SECONDARY COMMAND TABLE
	CALL	FDCOM	;TRY TO LOCATE IT
	JMP	DISP0	;OFF TO IT OR ERROR IF NOT IN TBL
;
;
;  THIS ROUTINE SETS THE TAPE SPEED
;
TASPD:	EQU	$	;GET CONVERTED VALUE
	ORA	A	;IS IT ZERO?
	JZ	SETSP	;YES--THAT IS A PROPER SPEED
	MVI	A,32	;NO--SET SPEED PROPERLY THEN
SETSP:	STA	TSPD
	RET
;
;
STSPD:	EQU	$	;VDM ESCAPE SEQUENCE COMES HERE
	MOV	A,B	;GET CHAR FOR FOLLOWING DISPD
DISPD:	EQU	$	;SET DISPLAY SPEED
	STA	SPEED
	RET
;
;
SETIN:	EQU	$	;SET AN INPUT PSUEDO PORT
	STA	IPORT
	RET
;
;
SETOT:	EQU	$	;SET AN OUTPUT PSUEDO PORT
	STA	OPORT
	RET
;
;
SETCI:	EQU	$	;DEFINE USER INPUT RTN ADDR
	SHLD	UIPRT
	RET
;
;
SETCO:	EQU	$	;DEFINE USER OUTPUT RTN ADDR
	SHLD	UOPRT
	RET
;
;
SETTY:	EQU	$	;SET TAPE HDR TYPE
	STA	HTYPE
	RET
;
;
SETXQ:	EQU	$	;SET TAPE-EXECUTE ADDDR FOR HDR
	SHLD	XEQAD
	RET
;
;
SETNU:	EQU	$	;HERE TO SET NUMBER OF NULLS
	STA	NUCNT	;THIS IS IT
	RET
;
;
SETCR:	EQU	$	;SET CRC TO BE NORMAL, OR IGNORE CRC ERRORS
	STA	IGNCR	;FF=IGNORE CRC ERRORS, ELSE=NORMAL
	RET
;
;
CUSET:	EQU	$	;TRY TO SET/CLEAR CUSTOM ROUTINE ADDR
	CALL	NAME0	;GET A NAME (S/B 2 CHARS OR MORE)
	LXI	H,COMND	;PT HERE IN CASE ADDR NOT GIVEN
	CALL	PSCAN	;GET OPTIONAL OPERAND IF ANY
	PUSH	H	;SAVE THAT VALUE (IF ANY)
	LXI	H,THEAD	;PT TO NAME
	CALL	FDCOU	;SEE IF NAME IS KNOWN IN CUST TABLE
	JZ	CUSE2	;NO--PROCEED TO KNOW IT
	DCX	D	;DE PT TO 1ST CHAR OF NAME IN TBL
	MVI	M,0	;(HL CAME BACK PT'ING TO THEAD)  CLR THIS NAME
CUSE2:	EQU	$	;ENTER NEW ONE IN TBL
	MOV	A,M	;GET 1ST CHAR OF NAME
	STAX	D	;PUT NAME INTO TABLE
	INX	D
	INX	H
	MOV	A,M	;GET 2ND CHAR OF NAME
	STAX	D	;NAME IS NOW POSTED
	INX	D	;PT TO 1ST BYTE OF ADDR
	POP	H	;RESTORE SAVED RTN ADDR
	XCHG		;DE=RTN ADDR, HL=THIS CU ENTRY
	MOV	M,E	;LO BYTE
	INX	H
	MOV	M,D	;AND HI BYTE
	RET		;ALL DONE
